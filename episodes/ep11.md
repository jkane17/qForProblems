### [Project Euler - Problem 11](https://projecteuler.net/problem=11)
### [Episode 11 - Part 1](https://www.youtube.com/watch?v=rA2OqaSQFUQ&list=PLsQYtymvFUhhft5F6IWzwEZ60dpB6MLMt&index=12)
### [Episode 11 - Part 2](https://www.youtube.com/watch?v=yay2vbYARwI&list=PLsQYtymvFUhhft5F6IWzwEZ60dpB6MLMt&index=13)

<br />

## Storing the grid

Store the [grid](../input/p11.txt) in *n*.

```q
// 20 x 20 grid
n:("J"$" " vs) each read0 `$"input/p11.txt"
// Number of adjacent numbers
x:4
```

<br />

# Solution 1

```q
// Number of rows/columns, plus one to account for extra 0 column that is joined
c:1+r:count n
// Column strided index (see episode 8 for details)
i:.util.cStrdIdx[r*c;x]
// Join zero to each row to 'cancel out' overlap
rn:raze n,\:0

// Each column is a group
// Horizontal 
rn i
// Vertical
raze[flip[n],\:0] i

// Diagonal index built by scaling i
ds:c*til x
// Scale leads to items out of range, so drop them
d:neg c*x-1

// Right diagonal - scaling in left to right direction
rn ds+d _/:i
// Left diagonal - scaling in right to left direction
raze[0,/:n](ds+x-2*til x)+d _/:i
```

All that remains is to join each of the four lists, perform the product, and take the max.

<br />

```q
s1:{[n;x]
    c:1+r:count n;
    i:.util.cStrdIdx[r*c;x];
    rn:raze n,\:0;
    ds:c*til x;
    d:neg c*x-1;

    max prd (,'/)(
        rn i;                            // Horizontal
        raze[flip[n],\:0] i;             // Vertical
        rn ds+d _/:i;                    // Right diagonal 
        raze[0,/:n](ds+x-2*til x)+d _/:i // Left diagonal
    )
 }

s1[n;x] // solution 1
```

<br />

# Solution 2

```q
// Number of rows/columns
r:count n
// Column strided index
i:.util.cStrdIdx[r;x]

// Row/column index
ri:til r
// Row index scaling
r*ri
// Horizontal
idx:i+/:r*ri

// First column index
r*i
// Vertical
idx,:(r*i)+/:ri

// Number of possible vertical moves
1+r-x
// Row starting index
d:r*til 1+r-x
// Element group index
xi:til x

// First row diagonals
i+r*xi
// Scale over each row
(i+r*xi)+/:d
// Right diagonal
idx,:(i+r*xi)+/:d

// Starting index
x-1
// Number of moves to next value
r-1
// Need to minus another one to account for indexing starting at 0
r-2
// Scaling for i
(x-1)+xi*r-2
// Left diagonal
idx,:(i+(x-1)+xi*r-2)+/:d

// Index into flattened n
raze[n] idx
// Find product - Each row contains the column groups to apply prd to
prd each raze[n] idx
// Flatten and find max
max raze prd each raze[n] idx
```

<br />

```q
s2:{[n;x]
    r:count n;
    i:.util.cStrdIdx[r;x];
    d:r*til 1+r-x;

    idx:i+/:r*til r;           // Horizontal
    idx,:(r*i)+/:til r;        // Vertical
    idx,:(i+r*xi:til x)+/:d;   // Right diagonal
    idx,:(i+(x-1)+xi*r-2)+/:d; // Left diagonal

    max raze prd each raze[n] idx
 }

s2[n;x] // solution 2
```

<br />

# Performance test

```q
// s1 more time efficient for this problem
delete args from .perf.test[10000;] each ((`s1;n;x);(`s2;n;x))

// Generate a random x * x matrix of numbers
g:{x#(prd x:2#x)?100}
// 10 x 10, 100 x 100, and 1000 x 1000
m:(enlist g@) each 10 100 1000

p:update args:(((count first@) each args[;0]),'args[;1]) from .perf.test[100;] each (cross/)(`s1`s2;m;4 8)

select from p where args[;0]=10
select from p where args[;0]=100
// s2 better for 1000 x 1000 matrix
select from p where args[;0]=1000
```

<br />

## Project Euler - Problem 10

For completeness, here is my solution to [Project Euler - Problem 10](https://projecteuler.net/problem=10).

See [here](https://community.kx.com/t5/Community-Blogs/Finding-primes-with-q/ba-p/11120) for details about `.math.primes`.

```q
sum .math.primes 2000000 // Project Euler #10 solution
```
