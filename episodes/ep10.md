### [Leet Code - Problem 1](https://leetcode.com/problems/two-sum/)
### [Episode 10](https://www.youtube.com/watch?v=bDBwGuKFbIE&list=PLsQYtymvFUhhft5F6IWzwEZ60dpB6MLMt&index=11)

<br />

# Solution 1

Calculate all possible sums and find which sum matches the target.

<br />

```q
// Inputs
t:9
n:2 7 11 15

// Triangle of indexes
(1+r)_\:r:til count n
// Index into n to get values
n (1+r)_\:r:til count n
// All possible sums
n+'n (1+r)_\:r:til count n
// Which sum is equal to the target
t=n+'n(1+r)_\:r:til count n
// The second index in the pair 
j:where each t=n+'n(1+r)_\:r:til count n
// The first index in the pair 
i:first where count each j
// Second index needs scaled
first[j i]+1+i
// Result
i,first[j i]+1+i
```

<br />

```q
s1:{[t;n] 
    r:til count n;
    j:where each t=n+'n(1+r)_\:r;
    $[count raze j;
        i,first[j i]+1+i:first where count each j;
        "j"$()
    ]
 }

s1[9;2 7 11 15]
s1[6;3 2 4]
s1[6;3 3]
s1[6;3 4]
```

<br />

Note : We need to do `first[j i]` rather than `j[i]` on its own because we might have the case where there are duplicates in the input *n*. For example

```q
// s1 as above
s1[9;2 7 7 11 15] // 0 1
// s1 with 'first[j i]' replaced with j[i]
s1[9;2 7 7 11 15] // 0 1 2 - Wrong!
```

<br />

# Solution 2 - Iterative approach

The idea of this solution is to iterate over the list of numbers and maintain a map of the numbers, seen thus far, to their index. At each iteration we check if the current number can be paired with a previous number to produce the target.

<br />

```q
t:9
n:2 7 11 15

// Map of numbers, seen thus far, to index 
m:(1#n)!1#0
// Current index = Previous index + 1
i:1+last m
// Current number from n
x:n i
// Check if the corresponding number (needed to reach target) has already been seen or not
m t-x
// A null value means no
null m t-x

// If NOT seen : return the updated map for the next iteration
m,(1#x)!1#i
// Else : return the indexes of the pair
m[t-x],i

// Check case where no pair is found
count[m]=count n
// Return empty list if so
"j"$()

// Predicate function : Continue whilst mapping (dict) is returned
99=type@
```

<br />

Putting this all together into a single function which iterates until a solution is found, or the list of numbers is exhausted.

<br />

```q
s2:{[t;n] 
    (99=type@){[t;n;m] 
        $[
            count[m]=count n;        // Checked all items and no pair was found
                "j"$(); 
            null m t-x:n i:1+last m; // Corresponding number not seen yet
                m,(1#x)!1#i; 
            m[t-x],i                 // Corresponding number has been seen, return index of pair
        ]
    }[t;n]/(1#n)!1#0
 }

s2[9;2 7 11 15]
s2[6;3 2 4]
s2[6;3 3]
s2[6;3 4]
```

<br />

# Performance Test

```q
// Generate test cases
g:{t:sum r:2#2*max n:neg[x]?10000000; n:@[n;2?x;:;r]; (t;n)}
// 5 of each and then we average as each case is random
c:g each raze 5#/:10 100 1000

p:.perf.test[1000;] each `s1`s2 cross c
p:update args:count each args[;1] from p
select "n"$avg average, "j"$avg mem by f, args from p
```
