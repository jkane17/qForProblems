### [Project Euler - Problem 14](https://projecteuler.net/problem=14)
### [Episode 14 - Part 1](https://www.youtube.com/watch?v=46bf0_4Jz5I)
### [Episode 14 - Part 2](https://www.youtube.com/watch?v=ZpQSa33V5uw)

<br />

# Solution 1

```q
// Next term in the Collatz sequence for integer x
collatzNext:{$[x<2; x; x mod 2; 1+3*x; x div 2]}

// Collatz sequence starting at 5
collatzNext 10 // 5
collatzNext 5  // 16
collatzNext 16 // 8
collatzNext 8  // 4
collatzNext 4  // 2
collatzNext 2  // 1

// Compute the Collatz sequence
collatz:collatzNext\

collatz 5 // 5 16 8 4 2 1
```

```q
// Max integer
x:10
// All integers from 1 to x inclusive
1+til x
// Collatz sequence starting at each of the integers
collatz each 1+til x
// Length of Collatz sequence starting at each of the integers
(count collatz@) each 1+til x
// Find the longest sequence
r?max r:(count collatz@) each 1+til x
// Add 1 to obtain actual value from index
1+r?max r:(count collatz@) each 1+til x
```

Note that this solution is simple, but is very slow (approx. 1 minute for input $999,999$).

```q
s1:{1+r?max r:(count collatz@) each 1+til x}
s1 999999 // solution 1
```

<br />

# Solution 2 - Vectorised Version

```q
vcollatzNext:{?[x=1; 1; ?[0=x mod 2; x div 2; 1+3*x]]}

vcollatzNext 1 2 3 4 5   // 1 1 10 2 16
vcollatzNext 1 1 10 2 16 // 1 1 5 1 8
vcollatzNext 1 1 5 1 8   // 1 1 16 1 4
vcollatzNext 1 1 16 1 4  // 1 1 8 1 2
vcollatzNext 1 1 8 1 2   // 1 1 4 1 1
vcollatzNext 1 1 4 1 1   // 1 1 2 1 1
vcollatzNext 1 1 2 1 1   // 1 1 1 1 1

// Use converge form of the scan operator
vcollatz:vcollatzNext\

vcollatz 1 2 3 4 5
```

```q
// Max integer
x:10
// All integers from 1 to x inclusive
1+til x
// All Collatz sequences
vcollatz 1+til x
// Check which sequence was last to converge
first -2#vcollatz 1+til x
1<>first -2#vcollatz 1+til x
?[;1b]1<>first -2#vcollatz 1+til x
// Scale the index to get the actual index
1+?[;1b]1<>first -2#vcollatz 1+til x
```

Note that this solution is faster than solution 1 (approx. 10 secs for input $999,999$), but has a high memory usage (approx. 4.5GB for input $999,999$).

```q
s2:1+?[;1b]1<>first -2#vcollatz 1+til@
s2 999999 // solution 2
```

<br />

# Solution 3

For even *n*

$$
\begin{align*}
    n \to {n \over 2} &\implies Collatz(n) = Collatz\bigg({n \over 2}\bigg) + 1 \\
    & \\
    &\implies Collatz(2k) > Collatz(k) \quad \forall k \\
    & \\
    &\implies \text{Only need to compute sequences for } {x \over 2} \le k \le x, \\
    &\qquad \quad \text{where } x \text{ is our maximum starting integer}
\end{align*}
$$

```q
// Maximum starting integer
x:10
// Integers we need to check
k+til 1+k:ceiling x%2
```

```q
s3:{k+?[;1b]1<>first -2#vcollatz k+til 1+k:ceiling x%2}
s3 999999 // solution 3
```

```q
// Gives us an approx. 2x speed-up and half memory usage compared to s2
delete args from .perf.test[10;] each `s2`s3 cross 100000
```

<br />

# Solution 4

If *n* is odd then $3n + 1$ is even

$$
\begin{align*}
    &\implies n \to 3n + 1 \to {3n + 1 \over 2} \\
    & \\
    &\implies Collatz(n) =  Collatz\bigg({3n + 1 \over 2}\bigg) + 2 \qquad \forall \text{ odd n}
\end{align*}
$$ 

```q
k:ceiling x%2
// n is current value of chain and c is length of chain
start:`n`c!(k+til 1+k;(k+1)#0)

// Continue with, values of 1 are considered "done"
cont:1<>start`n
// Spearate odd and evens
isodd:1=start[`n] mod 2
// Add 1 to count for even values and 2 for odd
start[`c]+:cont+cont and isodd;
// Update values
start[`n]:?[cont;?[isodd;1+3*start`n;start`n] div 2;1]

// Use over to repeat until result converges, i.e., all n is 1
r:({
    cont:1<>x`n;
    isodd:1=x[`n] mod 2;
    x[`c]+:cont+cont and isodd;
    x[`n]:?[cont;?[isodd;1+3*x`n;x`n] div 2;1];
    x
 }/) start

// Find the index of the longest chain
r[`c]?max r`c
// Scale index
k+r[`c]?max r`c
```

```q
s4:{
    k:ceiling x%2;
    start:`n`c!(k+til 1+k;(k+1)#0);

    r:({
        cont:1<>x`n;
        isodd:1=x[`n] mod 2;
        x[`c]+:cont+cont and isodd;
        x[`n]:?[cont;?[isodd;1+3*x`n;x`n] div 2;1];
        x
    }/) start;
    
    k+r[`c]?max r`c
 }

s4 999999 // solution 4
```

<br />

# Performance Test

```q
delete args from .perf.test[3;] each `s1`s2`s3`s4 cross 999999
```
