### [Project Euler - Problem 13](https://projecteuler.net/problem=13)
### [Episode 13](https://www.youtube.com/watch?v=k3vsFVekFfo)

<br />

```q
// Load 50 digit numbers
n:read0 `$"input/p13.txt"
```

<br />

# Solution 1

We first need to convert the matrix into long integer values.

```q
// Example input which is easier to view
ex:("123";"456";"789") 

// Convert each row implicitly
"J"$ex 
// Convert each row explicitly
"J"$'ex 
// Convert each value in each row
"J"$''ex 

// The last operation is what we want
"J"$''n
```

<br />

Next, we sum each column. This is essentially summing the digits in each decimal position, .i.e, sum all the 10s, 100s, 1000s, etc.

```q
// Example 
sum "J"$''ex

// The problem input
s:sum "J"$''n
```

For the problem input, the result is a list of longs, where the first value is in the $10^{49}$ position, the second value is in the $10^{48}$ position, etc.

We can therefore iterate through this list until we have enough digits in our summed result, and we can be sure that no further summing can affect these digits.

```q
// Start with the first two values in s
x:s 0   // In the position 10^49
y:s 1   // In the position 10^48
```

*x* is one order of $10$ larger than *y*.

To add *x* and *y*, we first need to scale *x* by a factor of $10$.

```q
y+10*x // In the position 10^48

// Use scan to iterate over s and keep track of the intermediate values
d:({y+10*x}\) s
```

At some point the values in *d* will overflow the boundary of a long integer and thus "wrap around" and become negative which, of course, is incorrect.

However, since we only need to continue until we
1) have the first $10$ digits; and
2) can be certain that adding any more terms will not affect these $10$ digits,

then we can stop and take the result at that point.

```q
// Has at least 14 digits (adding the 11th, 12th, and 13th terms will most likely affect the last few digits of the first 10 digits)
d>=1e13

// 11th digit is not a 9 as adding can cause a cascading effect which could affect the 10th digit
9<>(10 vs' d)[;10]

// Select the first number which satisfies these two requirements
d first where (d>=1e13) and 9<>(10 vs' d)[;10]

// Cast back to a string and take the first 10 digits to get the final result
10#string d first where (d>=1e13) and 9<>(10 vs' d)[;10]
```

<br />

```q
s1:{10#string d first where (d>=1e13) and 9<>(10 vs' d:({y+10*x}\) sum "J"$''x)[;10]}
s1 n // solution 1
```

<br />

This solution works in this case, but it might not work in the general case.

Consider that after summing the first few terms we should have a number like 

$12345678999999999999$

This number is beyond the limit of a long integer but, we can not yet guarentee that adding the next term will not affect some of the first $10$ digits (unless the next term is zero, it will affect).

Therefore, `s1` would not be able to find the final result. We need a general solution.

<br />

# Solution 2

The problem we face is storing, and adding to, a number larger than the limit of a long integer. 

We can overcome this if we instead store the number as a string and create a function which can add "number strings" together.

```q
// Input example
x:("123";"456";"78")

// Individual digit values
"J"$'/:x

// Want digits to line up
// Add left padding so all strings are the same length
neg[max count each x]$/:x

// If the input contains single character strings, e.g., 
x:("123";"456";"78";"9")
// then we need to make sure it is enlisted
(),/:x

// Altogether
("J"$'neg[max count each x]$(),)@/:x

// Fill empty locations with zero
0^("J"$'neg[max count each x]$(),)@/:x

// Sum each column
s:sum 0^("J"$'neg[max count each x]$(),)@/:x
```

Now we just need to ajust the digits to account for carry over, for example, if I have 
$$
5 \quad 1 \quad 13
$$

Then the $13$ can be reduced to $3$ and the $10$ is carried over to the next value on the left .

So the result would be
$$
5 \quad 2 \quad 3
$$

```q
// The carry over to the next term
d:s div 10
// Modulus with 10 to remove the carry term from its current position
// We do it manually rather than using mod to avoid doing div twice
s-10*d:s div 10
// Join extra left term incase there is a carry over from the current left most term
0,s-10*d:s div 10
// Perform carry
(d,0)+0,s-10*d:s div 10
// Remove left term if not needed
(0=first s)_s:(d,0)+0,s-10*d:s div 10

// Continue iterating until s converges incase adding the carry means an additional carry will be needed
({(0=first x)_x:(r,0)+0,x-10*r:x div 10}/) s

// Collapse to a single string
raze string ({(0=first x)_x:(r,0)+0,x-10*r:x div 10}/) s

// Add a list of number strings together
addStrs:{
    s:sum 0^("J"$'neg[max count each x]$(),)@/:x; 
    raze string ({(0=first x)_x:(d,0)+0,x-10*d:x div 10}/) s
 } 
```

```q
s2:10#addStrs@
s2 n // solution 2
```

<br />

# Performance Test

```q
// s2 is a general solution and is also more efficient
delete args from .perf.test[10000;] each(
    (`s1;n);
    (`s2;n)
 )
```
