### [Project Euler - Problem 12](https://projecteuler.net/problem=12)
### [Episode 12]()

<br />

# Solution 1

Since we do not know which number we need to check up until we must iterate until we find a number which satisfies the condition.

To derive the condition, we need to break an integer down into its factors (divisors).

```q
// Number we want to factorise
x:100
// List of positive integers up until and including x
n:1+til x
// Check all n to see which divide evenly into x
0=x mod n
// Add 1 index to get the factors
1+where 0=x mod n

factor0:{1+where 0=x mod n:1+til x}

factor0 100
```

<br />

The time complexity of `factor0` is $O(n)$.

We can improve on this if we notice that factors must come in pairs, e.g., $4$ is a factor of $100$ because $4 \times 25 = 100$, and $25$ is a factor of $100$ because $25 \times 4 = 100$.

So, $4$ and $25$ are a factor pair.

For the same reason as stated in [episode 3](ep3.md) when we discussed prime factorisation, we only need to check up until the square root of *x*.

```q
// List of positive integers up until the square root of x
n:1+til ceiling sqrt x
// Factors up until the square root
n:1+where 0=x mod n
// Corresponding value for each factor to complete each pair
x div n
// Join together
n,x div n
// May have duplicates, e.g., 10 x 10 = 100, so need to distinct the list
distinct n,x div n
```

<br />

Time complexity : $O(\sqrt n)$

```q
factor:{distinct n,x div n:1+where 0=x mod n:1+til ceiling sqrt x}

// Note that the output is not sorted but that does not matter for this problem
factor 100
```

`factor` is much more efficient than `factor0`.

```q
.perf.test[1000;] each `factor0`factor cross 100 10000 1000000
```

```q
// Predicate function to check if number of factors is greater than 500
(500>count factor last@)

// Input is a 2 element list (we start at (1;0))
// The first element is the current number we are on 
// The second element holds the result to be factored
(1;0)

// Each iteration adds the next number to the previous result to
// get the next result and also increments the current number
{(1+x 0;x[1]+x 0)}/(1;0)
```

```q
s1:{last(x>count factor last@){(1+x 0;x[1]+x 0)}/(1;0)};
s1 500 // solution 1
```

<br />

# Solution 2

The number of factors of an integer *N* can be computed via

$$
\begin{equation}
    F(N) = ( a_1 + 1 ) \times ( a_2 + 1 ) \times ... \times ( a_n + 1 )
\end{equation}
$$

where $a_n$ are the exponents of the distinct prime factors of *N*.

<br />

For example, if $N = 100$, then the prime factorisation of *N* is

$$
    P(100) = 2 \times 2 \times 5 \times 5 = 2^2 \times 5^2 
$$

and so the number of factors can be found using equation **(1)**

$$
    F(100) = ( 2 + 1 ) * ( 2 + 1 ) = 9
$$

which is correct since $100$ does indeed have $9$ factors ($1$ $2$ $4$ $5$ $10$ $20$ $25$ $50$ $100$).

<br />

Using the `pfact` function described in [episode 3](ep3.md), we can compute the number of factors.

```q
// Prime factorisation
.math.pfact 100
// Group into distinct primes
group .math.pfact 100
// Exponents of each distinct prime factor
count each group .math.pfact 100
// Add 1 to each exponent
1+count each group .math.pfact 100
// Find the product
prd count each group .math.pfact 100

// Must also add safety for the number 1

// Number of factors a positive integer has
nfactors:{$[x<2;x;prd 1+count each group .math.pfact x]}

nfactors 100 // 9
```

<br />

The $n^{th}$ triangle number can be computed as the sum of the first $n$ natural numbers.

$$
\begin{equation}    
    S_n = n * ( n + 1 ) / 2
\end{equation}  
$$

We previously covered this function in [episode 6](ep6.md), and so we can use it here to compute the $n^{th}$ triangle number.

```q
// The nth triangle number
triNum:.math.nsum
// The 7th triangle number
triNum 7
```

<br />

<div style="background-color:CornflowerBlue">
<h3>Side Note</h3>
We can easily compute the first <i>n</i> triangle numbers.

```q
// First n triangle numbers
triNums:triNum 1+til@

// First 10 triangle numbers
triNums 10 // 1 3 6 10 15 21 28 36 45 55
```
</div>

<br />

Since $n$ and $(n+1)$, from equation **(2)**, must be co-prime (have no common prime factors and therefore no common factors) then the total number of factors of a triangle number *T* can be computed as

$$
    F(T) = 
        \begin{cases}
            F(n + 1) \times F({n \over 2}) &\text{if } n \text{ even}  \\
            F(n) \times F({n + 1 \over 2}) &\text{if } n \text{ odd} 
        \end{cases}
$$

<br />

Therefore, the number of factors for the $n^{th}$ triangle number can be computed as

```q
// Decide ordering of n and n+1 based on n being odd or even 
$[n mod 2; n,n+1; (n+1),n]
// Compute the two inputs to F
{x,y div 2}. $[n mod 2; n,n+1; (n+1),n]
// Apply F to each input
nfactors each {x,y div 2}. $[n mod 2; n,n+1; (n+1),n]
// Take the product
prd nfactors each {x,y div 2}. $[n mod 2; n,n+1; (n+1),n]

// Number of factors for the nth triangle number
nTriFactors:{prd nfactors each {x,y div 2}. $[x mod 2; x,x+1; (x+1),x]}

// Number of factors for the 7th triangle number
nTriFactors 7
```

<br />

```q
// Predicate function to check if number of factors is greater than 500
(500>last@)
// Starting point same as solution 1
(1;0)
// Increment n by 1 and then find the number of factors, then return the updated list
{(n;nTriFactors n:1+x 0)}
// Select n
first (500>last@){(n;nTriFactors n:1+x 0)}/(1;0)
// Calculate the nth triangle number
triNum first (500>last@){(n;nTriFactors n:1+x 0)}/(1;0)
```

<br />

```q
s2:{triNum first (x>last@){(n;nTriFactors n:1+x 0)}/(1;0)};
s2 500 // Solution 2
```

<br />

# Performance test

```q
.perf.test[10;] each `s1`s2 cross 100 500 1000
```
